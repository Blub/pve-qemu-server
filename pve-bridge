#!/usr/bin/perl -w

use strict;
use PVE::QemuServer;
use PVE::Tools qw(run_command);
use PVE::Network;

my $iface = shift;

die "no interface specified\n" if !$iface;

die "got strange interface name '$iface'\n" 
    if $iface !~ m/^tap(\d+)i(\d+)$/;

my $vmid = $1;
my $netid = "net$2";

my $migratedfrom = $ENV{PVE_MIGRATED_FROM};

my $conf = PVE::QemuServer::load_config($vmid, $migratedfrom);

die "unable to get network config '$netid'\n"
    if !$conf->{$netid};

my $net = PVE::QemuServer::parse_net($conf->{$netid});
die "unable to parse network config '$netid'\n" if !$net;

my $bridge = $net->{bridge};
die "unable to get bridge setting\n" if !$bridge;

my $bridgemtu = PVE::Tools::file_read_firstline("/sys/class/net/$bridge/mtu");
die "bridge '$bridge' does not exist\n" if !$bridgemtu;

system ("/sbin/ifconfig $iface 0.0.0.0 promisc up mtu $bridgemtu") == 0 ||
    die "interface activation failed\n";

PVE::Network::tap_rate_limit($iface, $net->{rate}) if $net->{rate};

my $newbridge = PVE::Network::activate_bridge_vlan($bridge, $net->{tag});
PVE::Network::copy_bridge_config($bridge, $newbridge) if $bridge ne $newbridge;

system ("/usr/sbin/brctl addif $newbridge $iface") == 0 ||
    die "can't add interface to bridge\n";

exit 0;
