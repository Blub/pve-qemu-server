#!/usr/bin/perl

use strict;
use warnings;
use PVE::QemuServer;
use PVE::Tools qw(run_command);
use PVE::Network;

my $iface = shift;

die "no interface specified\n" if !$iface;

die "got strange interface name '$iface'\n" 
    if $iface !~ m/^tap(\d+)i(\d+)$/;

my $vmid = $1;
my $netid = "net$2";

my $migratedfrom = $ENV{PVE_MIGRATED_FROM};

my $conf = PVE::QemuServer::load_config($vmid, $migratedfrom);

my $netconf = $conf->{$netid};

$netconf = $conf->{pending}->{$netid} if !$migratedfrom && defined($conf->{pending}->{$netid}); 
 
die "unable to get network config '$netid'\n"
    if !defined($netconf);

my $net = PVE::QemuServer::parse_net($netconf);
die "unable to parse network config '$netid'\n" if !$net;

PVE::Network::tap_create($iface, $net->{bridge});

PVE::Network::tap_rate_limit($iface, $net->{rate}) if $net->{rate};

PVE::Network::tap_plug($iface, $net->{bridge}, $net->{tag}, $net->{firewall});

exit 0;
